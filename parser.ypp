%{
  #include <stdio.h>
  #include <stdlib.h>
  #include <string>
  #include <map>
  #include <iostream>

  #include "node.h"

  std::map<std::string,void*> symbolTable;

  program* root;

  extern int yylex();
  void endProgram();
  void yyerror(std::string errmsg);
%}
%define parse.error verbose
%define parse.lac full
%union {
  double fval;
  int ival;
  std::string *sval;
  list *lval;
  def *dval;
  class_def *cval;
  statement *stval;
  expression *eval;
  func_call *fcval;
  import *imval;
  assign *aval;
  static_assign *saval;
  name *nval;
  const_expr *ceval;
  if_stmnt *ifval;
}
%token <sval> NAME
%token <sval> STRING
%token <ival> NUMBER
%token <fval> FNUMBER
%token THIS
%token TRUE
%token FALSE
%token NEW
%token NEWLINE
%token CLASS
%token DEF
%token IF
%token ELSE
%token FOR
%token WHILE
%token IMPORT

/*%type <lval> program;*/
%type <dval> def_stmnt;
%type <ifval> if_stmnt;
%type <lval> imports;
%type <imval> import;
%type <lval> class_stmnts;
%type <cval> class_stmnt;
%type <lval> static_assigns;
%type <saval> static_assign;
%type <ceval> const_expr;
%type <lval> class_body
%type <lval> parameters;
%type <lval> names;
%type <lval> block;
%type <lval> body;
%type <lval> lines;
%type <lval> line;
%type <lval> args;
%type <lval> exprs;
%type <eval> term;
%type <ceval> literal;
%type <stval> stmnt;
%type <eval> expr2;
%type <fcval> func_call;
%type <eval> expr;
%type <aval> assignment;
%type <nval> identifier;

%%
program:  imports class_stmnts { root = new program($1,$2); }
       ;

eol: NEWLINE | NEWLINE eol;

imports:  /* NOTHING */   { $$ = new list(); }
       |  import imports  { $$ = new list($1,$2); }
       ;

import: IMPORT NAME eol { $$ = new import(*$2); }
      ;

class_stmnts: class_stmnt               { $$ = new list($1); }
            | class_stmnt class_stmnts  { $$ = new list($1, $2); }
            ;

class_stmnt: CLASS NAME '{' eol static_assigns class_body '}' eol { $$ = new class_def(*$2,$5,$6); }
           ;

static_assigns: /* NOTHING */                 { $$ = new list(); }
              | static_assign static_assigns  { $$ = new list($1,$2); }
              ;

static_assign: NAME '=' const_expr eol  { $$ = new static_assign(*$1, $3);}
             ;

const_expr: NAME    { $$ = new name(*$1); }
          | literal { $$ = $1; }

class_body: def_stmnt             { $$ = new list($1); }
          | def_stmnt class_body  { $$ = new list($1,$2); }
          ;

def_stmnt: DEF NAME '(' parameters ')' block eol  { $$ = new def(*$2,$4,$6); }
         ;

parameters: /* NOTHING */ { $$ = new list(); }
          | names         { $$ = $1; }
          ;

names:  NAME            { $$ = new list(new name(*$1)); }
     |  NAME ',' names  { $$ = new list(new name(*$1),$3); }
     ;

block:  '{' body '}'  { $$ = $2; }
     ;

body: /* NOTHING */ { $$ = new list(); }
    | lines         { $$ = $1; }
    | line          { $$ = $1; }
    ;

lines:  eol line eol { $$ = $2; }
     |  eol line lines   { $$ = new list($2,$3); }
     ;

line: stmnt           { $$ = new list($1); }
    | stmnt ';'       { $$ = new list($1); }
    | stmnt ';' line  { $$ = new list($1,$3); }
    ;

stmnt:  if_stmnt    { $$ = $1; }
     |  for_stmnt   { $$ = NULL; }
     |  while_stmnt { $$ = NULL; }
     |  expr        { $$ = $1; }
     ;

if_stmnt: IF '(' expr ')' block             { $$ = new if_stmnt($3,$5); }
        | IF '(' expr ')' block ELSE block  { $$ = new if_stmnt($3,$5,$7); }
        ;

for_stmnt: FOR
         ;

while_stmnt: WHILE
           ;

expr: identifier  { $$ = $1; }
    | expr2       { $$ = $1; }
    | assignment  { $$ = $1; }
    | THIS        { $$ = new this_term(); }
    ;

expr2: func_call  { $$ = $1; }
     | term       { $$ = $1; }
     ;

func_call:  NAME '.' NAME '(' args ')'  { $$ = new func_call(new name(*$1), *$3, $5); }
         |  NAME '.' NEW '(' args ')'   { $$ = new func_call(new name(*$1), "new", $5); }
         |  THIS '.' NAME '(' args ')'   { $$ = new func_call(new this_term(), *$3, $5); }
         |  expr2 '.' NAME '(' args ')' { $$ = new func_call($1, *$3, $5); }
         ;

args: /* NOTHING */ { $$ = new list(); }
    | exprs         { $$ = $1; }
    ;

exprs: expr           { $$ = new list($1); }
     | expr ',' exprs { $$ = new list($1,$3); }
     ;

assignment: identifier '=' expr { $$ = new assign($1,$3); }
          ;

term: literal       { $$ = $1; }
    | '(' expr ')'  { $$ = $2; }
    ;

literal: STRING   { $$ = new string_term(*$1); }
       | NUMBER   { $$ = new int_term($1); }
       | FNUMBER  { $$ = new float_term($1); }
       | TRUE     { $$ = new bool_term(true); }
       | FALSE    { $$ = new bool_term(false); }
       ;

identifier: NAME          { $$ = new name(*$1); }
          | '.' NAME      { $$ = new name(*$2, true); }
          | THIS '.' NAME { $$ = new name(*$3, true); }
          ;

%%
void yyerror(std::string errmsg)
{
  std::cerr << errmsg << std::endl;
}
